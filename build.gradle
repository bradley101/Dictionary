buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}
plugins {
    id 'java-library'
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'com.bradley101.dictionary'
version = '1.0'

repositories {
    jcenter()
}

dependencies {

    api 'org.apache.commons:commons-math3:3.6.1'

    implementation 'com.google.guava:guava:28.0-jre'

    testImplementation 'junit:junit:4.12'
}
archivesBaseName = 'com.bradley101.dictionary'
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "bradley101"
            name "Shantanu Banerjee"
            email "shantanu.banerjeee@gmail.com"
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A gradle plugin for multiple key to value map objects')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'Dictionary'
        name = 'Dictionary'
        userOrg = 'bradley101'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bradley101/Dictionary.git'
        version {
            name = '1.0-final'
            desc = 'Dictionary library v1.0'
            released  = new Date()
            vcsTag = version
        }
    }
}
